cmake_minimum_required(VERSION 3.12 FATAL_ERROR)
file(STRINGS "VERSION" VERSION_STRING)
project(ntopng VERSION "${VERSION_STRING}" LANGUAGES CXX C)
set(CMAKE_BUILD_TYPE_INIT Release)
set(CMAKE_CXX_STANDARD 20)
set(CMAKE_CXX_STANDARD_REQUIRED ON)
set(CMAKE_CXX_EXTENSIONS OFF)
set(NTOP_CMAKE_BUILD TRUE)
if (CMAKE_COMPILER_IS_GNUCXX) 
  message("-- GNU++ Version: ${CMAKE_CXX_COMPILER_VERSION}")
  if (CMAKE_CXX_COMPILER_VERSION VERSION_LESS 11.1)
    message(FATAL_ERROR "-- Please update the compiler to GCC 11")
  else()
   message("-- Found modern C++ compiler. Enabling C++20")
  endif()
else()
  if (CMAKE_CXX_COMPILER_ID STREQUAL "Clang")
    if (CMAKE_CXX_COMPILER_VERSION VERSION_LESS 13)
      message(FATAL_ERROR "-- Please update the compiler to Clang 13")
      set(CMAKE_CXX_STANDARD 03)
    else()
      message("Found modern C++ compiler. Enabling C++20")
    endif()
  endif()
endif()

if(NOT CMAKE_BUILD_TYPE)
  set(CMAKE_BUILD_TYPE Debug)
endif()
set(CMAKE_MODULE_PATH ${CMAKE_MODULE_PATH} ${CMAKE_SOURCE_DIR}/cmake)

find_package(Threads REQUIRED)
find_package(OpenSSL REQUIRED)  
find_package(ZeroMQ REQUIRED)
find_package(PCAP REQUIRED)
find_package(Ndpi REQUIRED)
find_package(Lua REQUIRED)
find_package(JsonC REQUIRED)
find_package(SQLite3 REQUIRED)
find_package(Hiredis REQUIRED)
find_package(Mongoose REQUIRED)
set(LOCAL_INCLUDE ${CMAKE_SOURCE_DIR}/include)
include_directories(include ${OPENSSL_INCLUDE_DIR} ${HIREDIS_INCLUDE_DIR} ${SQLite3_INCLUDE_DIRS} ${ZeroMQ_INCLUDE_DIRS} ${NDPI_INCLUDE_DIR} ${PCAP_INCLUDE_DIR} ${JSONC_INCLUDE_DIR} ${LUA_INCLUDE_DIR} ${MONGOOSE_INCLUDE_DIR})

file(GLOB_RECURSE INCLUDE_FILES ${CMAKE_SOURCE_DIR}/include/*.h)
file(GLOB_RECURSE SOURCE_FILES_NO_MAIN ${CMAKE_SOURCE_DIR}/src/*.cpp)
# for using catch we don't need the main
list(FILTER SOURCE_FILES_NO_MAIN EXCLUDE REGEX ".*main.cpp$")
#include(${CMAKE_SOURCE_DIR}/cmake/unit_test.cmake)
include(${CMAKE_SOURCE_DIR}/cmake/clang_tools.cmake)
#include(${CMAKE_SOURCE_DIR}/cmake/cppcheck.cmake)

#if(CMAKE_BUILD_TYPE MATCHES "^[Rr]elease")
#include(${CMAKE_SOURCE_DIR}/cmake/doxygen.cmake)
#endif()
add_executable(ntopng ${SOURCE_FILES_NO_MAIN} ${MONGOOSE_SRC_FILES} ${CMAKE_SOURCE_DIR}/src/main.cpp)
if (CMAKE_CXX_COMPILER_ID STREQUAL "Clang")
  # using Clang
  set(CMAKE_EXE_LINKER_FLAGS "${CMAKE_EXE_LINKER_FLAGS} -stdlib=libc++ -lc++abi")
  if(CMAKE_BUILD_TYPE STREQUAL "Debug")
    target_compile_options(ntopng PRIVATE -Wall -Wextra -Werror -stdlib=libc++ -fsanitize=address -fprofile-arcs -ftest-coverage -fno-omit-frame-pointer -v)
  else()
    target_compile_options(ntopng PRIVATE -Wall -Wextra -Werror -stdlib=libc++ -v)
  endif()
elseif (CMAKE_CXX_COMPILER_ID STREQUAL "GNU")
  # using GCC
  if(CMAKE_BUILD_TYPE STREQUAL "Debug")
  target_compile_options(ntopng PRIVATE -Wall -Wextra -Werror -O0 -ggdb -fprofile-arcs -ftest-coverage)
  set(CMAKE_EXE_LINKER_FLAGS "${CMAKE_EXE_LINKER_FLAGS} -lgcov -coverage")
  else()
  target_compile_options(ntopng PRIVATE -Wall -Wextra -Werror)
  endif()
endif()
target_compile_definitions(ntopng PRIVATE NTOP_CMAKE_BUILD=${NTOP_CMAKE_BUILD})
target_link_libraries(ntopng PRIVATE ${OPENSSL_LIBRARIES} ${ZeroMQ_LIBRARIES}  ${NDPI_LIBRARY} ${PCAP_LIBRARY} ${NDPI_LIBRARY} ${LUA_LIBRARIES}  ${JSONC_LIBRARY} ${SQLite3_LIBRARIES} ${HIREDIS_LIBRARIES} Threads::Threads)
